plugins {
    id "application"
}

configurations {
    include

    implementation.extendsFrom include
    api.extendsFrom include
}

repositories {
    maven {
        name = "lenni0451"
        url = "https://maven.lenni0451.net/everything"
    }
}

dependencies {
    include project(":")

    include "com.formdev:flatlaf:3.3"
    include "net.lenni0451.commons:swing:1.5.0-SNAPSHOT"
    include "org.lwjgl:lwjgl:3.3.3"
    include "org.lwjgl:lwjgl-openal:3.3.3"
    ["natives-windows", "natives-windows-arm64", "natives-linux", "natives-linux-arm64", "natives-macos", "natives-macos-arm64"].each {
        include "org.lwjgl:lwjgl:3.3.3:$it"
        include "org.lwjgl:lwjgl-openal:3.3.3:$it"
    }
}

application {
    mainClass = "net.raphimc.noteblocklib.gui.Main"
}

jar {
    dependsOn configurations.include
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.include.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }

    manifest {
        attributes(
                "Main-Class": application.mainClass,
                "Multi-Release": "true"
        )
    }
}
